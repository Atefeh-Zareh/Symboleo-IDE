grammar ca.uottawa.csmlab.symboleo.Symboleo with org.eclipse.xtext.common.Terminals

generate symboleo "http://www.uottawa.ca/csmlab/symboleo/Symboleo"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	'Domain' domainName=ID
	(domainTypes+=DomainType ';')+
	'endDomain'
	'Contract' contractName=ID '(' (parameters+=Parameter ',')+ (parameters+=Parameter) ')'
	('Declarations' (variables+=Variable ';')*)?
	('Preconditions' (preconditions+=Proposition ';')*)?
	('Postconditions' (postconditions+=Proposition ';')*)?
	('Obligations' (obligations+=Obligation ';')*)+
	('Surviving' 'Obligations' (survivingObligations+=Obligation ';')*)?
	('Powers' (powers+=Power ';')*)?
	('Constraints' (constraints+=Proposition ';')*)?
	'endContract';

DomainType:
	Alias | RegularType | Enumeration;

Alias:
	name=ID 'isA' type=BaseType;

Enumeration:
	name=ID 'isAn' 'Enumeration' '(' (enumerationItems+=EnumItem ',')* (enumerationItems+=EnumItem) ')';

EnumItem:
	name=ID;

// TODO not extend itself, prevent cycles
// TODO check attribute names to be unique, (parent classes)
RegularType:
	name=ID ('isA' | 'isAn') ontologyType=OntologyType ('with' (attributes+=Attribute ',')* (attributes+=Attribute))? |
	name=ID ('isA' | 'isAn') regularType=[RegularType] ('with' (attributes+=Attribute ',')* (attributes+=Attribute))?;

// TODO check attribute modifier is only used for type event 
Attribute:
	attributeModifier=AttributeModifier? name=ID ':' baseType=BaseType |
	attributeModifier=AttributeModifier? name=ID ':' domainType=[DomainType];

BaseType:
	name=("Number" | "String" | "Date" | "Boolean");

OntologyType:
	name=("Asset" | "Event" | "Role" | "Contract");

AttributeModifier:
	name=('Env');

Parameter:
	name=ID ':' type=ParameterType;

ParameterType:
	baseType=BaseType |
	domainType=[DomainType];

Variable:
	name=ID ':' type=[RegularType] ('with' attributes+=Assignment (',' attributes+=Assignment)*)?;

// TODO name should be from the attributes of the used type
// TODO assign from parameters and variables
// TODO check assigned type is correct
Assignment returns AssignVariable:
	{AssignVariable} name=ID ':=' value=VariableDotExpression |
	AssignmentB;

VariableDotExpression returns Ref:
	VariableRef ({VariableDotExpression.ref=current} "." tail=[Attribute])*;

VariableRef returns Ref:
	{VariableRef} variable=ID;

AssignmentB returns AssignExpression:
	{AssignExpression} name=ID ':=' value=Expression;

Double returns ecore::EDouble:
	INT '.' INT;

// TODO time expressions
// TODO dot expressions
// TODO check forward reference
// TODO declared variables
// TODO no type cast
// TODO enum values
Expression: Or;

Or returns Expression:
	And ({Or.left=current} "or" right=And)*;

And returns Expression:
	Equality ({And.left=current} "and" right=Equality)*;

Equality returns Expression:
	Comparison ({Equality.left=current} op=("==" | "!=")
	right=Comparison)*;

Comparison returns Expression:
	Addition ({Comparison.left=current} op=(">=" | "<=" | ">" | "<") right=Addition)*;

Addition returns Expression:
	Multiplication (({Plus.left=current} '+' | {Minus.left=current} '-') right=Multiplication)*;

Multiplication returns Expression:
	PrimaryExpression (({Multi.left=current} '*' | {Div.left=current} '/') right=PrimaryExpression)*;

PrimaryExpression returns Expression:
	'(' Expression ')' |
	{PrimaryExpressionFunctionCall} function=FunctionCall |
	{NegatedPrimaryExpression} "not" expression=PrimaryExpression |
	AtomicExpression
;

AtomicExpression returns Expression:
	{AtomicExpressionDouble} value=Double |
	{AtomicExpressionInt} value=INT |
	{AtomicExpressionString} value=STRING |
	{AtomicExpressionTrue} value="false" |
	{AtomicExpressionFalse} value="true" |
	{AtomicExpressionParameter} value=VariableDotExpression
;

FunctionCall:
	MathFunction | StringFunction | TimeFunction
;

MathFunction:
	name=('Math.pow') '(' arg1=Expression ',' arg2=Expression ')' |
	name=('Math.abs') '(' arg1=Expression ')' |
	name=('Math.floor') '(' arg1=Expression ')';

StringFunction:
	name=('String.concatenate') '(' arg1=Expression ',' arg2=Expression ')' |
	name=('String.lowerCase') '(' arg1=Expression ')';
	
TimeFunction:
	'Time.function()'		
;

// TODO check that creditor and debtor are valid references
Obligation:
	name=ID ':' (trigger=Proposition '->')? ('O' | 'Obligation') '(' debtor=ID ',' creditor=ID ','
	antecedent=Proposition ',' consequent=Proposition ')';

Power:
	name=ID ':' (trigger=Proposition '->')? ('P' | 'Power') '(' creditor=ID ',' debtor=ID ',' antecedent=Proposition ','
	consequent=Proposition ')';

Proposition:
	junctions+=PJunction ('or' junctions+=PJunction)*;

PJunction:
	equalityExps+=PEquality ('and' equalityExps+=PEquality)*;
	
PEquality:
	PComparison ({PEquality.left=current} op=("==" | "!=") right=PComparison)*;

PComparison:
	PNegation ({PComparison.left=current} op=(">=" | "<=" | ">" | "<") right=PNegation)*;

PNegation:
	negated?=('not')? atomicExpression=PAtom;

PAtom:
	'(' inner=Proposition ')' |
	{PAtomPredicate} predicateFunction=PredicateFunction |
	{PAtomVariable} variable=VariableDotExpression |
	{PAtomPredicateTrueLiteral} value='true' |
	{PAtomPredicateFalseLiteral} value='false' |
	{PAtomIntLiteral} value=INT |
	{PAtomStringLiteral} value=STRING
	;

// TODO check cannotBeAssigned arg type
PredicateFunction:
	name='Happens' '(' event=Event ')' |
	name='HappensBefore' '(' event=Event ',' point=Point ')' |
	name='HappensAfter' '(' event=Event ',' point=Point ')' |
	name='HappensWithin' '(' event=Event ',' interval=Interval ')' |
	name='Occurs' '(' situation=Situation ',' interval=Interval ')' |
	name='IsEqual' '(' arg1=ID ',' arg2=ID ')' |
	name='IsOwner' '(' arg1=ID ',' arg2=ID ')' |
	name='CannotBeAssigned' '(' arg1=ID ')'
	;

Event:
	{VariableEvent} variable=VariableDotExpression
	| obligationEvent=ObligationEvent
	| contractEvent=ContractEvent
	| powerEvent=PowerEvent;

PowerEvent:
	eventName=PowerEventName '(' powerVariable=[Power] ')';

PowerEventName:
	'Triggered' | 'Activated' | 'Suspended' | 'Resumed' | 'Exerted' | 'Expired' | 'Terminated';

ObligationEvent:
	eventName=ObligationEventName '(' obligationVariable=[Obligation] ')';

ObligationEventName:
	'Triggered' | 'Activated' | 'Suspended' | 'Resumed' | 'Discharged' | 'Expired' | 'Fulfilled' | 'Violated' |
	'Terminated';

ContractEvent:
	eventName=ContractEventName '(' 'self' ')';

ContractEventName:
	'Activated' | 'Suspended' | 'Resumed' | 'FuflfilledObligations' | 'RevokedParty' | 'AssignedParty' | 'Terminated' |
	'Rescinded';

Point:
	pointExpression=PointExpression;

PointExpression:
	pointFunction=PointFunction |
	pointAtom=PointAtom;

PointFunction:
	name=PointFunctionName '(' pointExpression=PointExpression ',' value=Timevalue ',' timeUnit=TimeUnit ')';

PointFunctionName:
	'addTime';

PointAtom:
	{PointAtomParameterDotExpression} variable=VariableDotExpression | 
	{PointAtomObligationEvent} obligationEvent=ObligationEvent |
	{PointAtomContractEvent} contractEvent=ContractEvent |
	{PointAtomPowerEvent} powerEvent=PowerEvent;


Timevalue:
	value=INT |
	variable=VariableDotExpression // TODO check type of variable
;

TimeUnit:
	'seconds' | 'minutes' | 'hours' | 'days' | 'weeks' | 'months' | 'years';

Interval:
	intervalExpression=IntervalExpression;

IntervalExpression:
	intervalFunction=IntervalFunction |
	intervalAtom=Situation;

IntervalFunction:
	name=IntervalFunctionName '(' pointExpression=PointExpression ',' pointExpression2=PointExpression ')';

IntervalFunctionName:
	'Interval';

Situation:
	obligationState=ObligationState |
	contractState=ContractState |
	powerState=PowerState;

PowerState:
	eventName=PowerStateName '(' powerVariable=[Power] ')';

PowerStateName:
	'Create' | 'UnsuccessfulTermination' | 'Active' | 'InEffect' | 'Suspension' | 'SuccessfulTermination';

ObligationState:
	eventName=ObligationStateName '(' obligationVariable=[Obligation] ')';

ObligationStateName:
	'Create' | 'Discharge' | 'Active' | 'InEffect' | 'Suspension' | 'Violation' | 'Fulfillment' |
	'UnsuccessfulTermination';

ContractState:
	eventName=ContractStateName '(' 'self' ')';

ContractStateName:
	'Form' | 'UnAssign' | 'InEffect' | 'Suspension' | 'Rescission' | 'SuccessfulTermination' | 'UnsuccessfulTermination'
	| 'Active';
	

// TODO dot navigation for ontology models
	
